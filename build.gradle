//
// Copyright (c) 2016, Brian Frank
// Licensed under the Academic Free License version 3.0
//
// History:
//   10 Jun 2016  Matthew Giannini  Creation
//
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'war'

version = '3.0.2'

compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.akhikhl.gretty:gretty:2.0.0'
    }
}

repositories {
    jcenter()
}

apply plugin: 'org.akhikhl.gretty'

dependencies {

    compile 'javax.servlet:servlet-api:2.5'

    // Use TestNG framework, also requires calling test.useTestNG() below
    testCompile 'org.testng:testng:6.9.11'

    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
}

// Stupid war plugin disable jar for some reason. Force jar creation
assemble.dependsOn(jar)


test {
    useTestNG() {
        outputDirectory = file("$project.buildDir/reports/testng")
        useDefaultListeners = true
    }

    testLogging.showStandardStreams = true;

    testLogging {
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }
}

gretty {
    contextPath = 'haystack'
}

task wrapper(type: Wrapper) { gradleVersion = '3.4.1' }

// Maven pom generation
task createPom {
    outputs.file("pom.xml")
    outputs.upToDateWhen { false }

    doLast {
        pom {
            project {
                groupId 'org.projecthaystack'
                artifactId 'haystack-java'
                version project.version - "-SNAPSHOT"
            }
        }.writeTo("pom.xml")
    }
}
compileJava { doLast { tasks.createPom.execute() } }
clean { doLast { tasks.cleanCreatePom.execute() } }

// Not sure if this is best way to handle SNAPSHOT versions, but it works for now
task release(dependsOn: build) { doLast { } }

gradle.taskGraph.whenReady {taskGraph ->
    if (!taskGraph.hasTask(release)) version += "-SNAPSHOT"
}
