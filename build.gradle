//
// Copyright (c) 2016, Brian Frank
// Licensed under the Academic Free License version 3.0
//
// History:
//   10 Jun 2016  Matthew Giannini  Creation
//
plugins {
    id 'java-library'
    id 'maven-publish'
    id 'war'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'idea'
}

group = "org.projecthaystack"
version = "3.0.4"

java {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7

//    withJavadocJar()
    withSourcesJar()
}


repositories {
    jcenter()
}

dependencies {
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'

    // Use TestNG framework, also requires calling test.useTestNG() below
    testImplementation 'org.testng:testng:7.1.0'
}

test {
    useTestNG() {
        outputDirectory = file("$project.buildDir/reports/testng")
        useDefaultListeners = true
    }

    testLogging.showStandardStreams = true;

    testLogging {
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }
}

// Stupid war plugin disable jar for some reason. Force jar creation
assemble.dependsOn(jar)

publishing {
    publications {
        create("haystack-java", MavenPublication) {
            artifactId = 'haystack-java'

            from components.java
        }
    }
}

String findProperty(String s) { project.findProperty(s) }

bintray {
    user = findProperty("bintrayUser")
    key  = findProperty("bintrayApiKey")
    publish = true
    setPublications("haystack-java")
    pkg {
        repo    = 'maven'
        name    = 'haystack-java'
        userOrg = 'skyfoundry'
        vcsUrl  = 'https://bitbucket.org/brianfrank/haystack-java'

    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

//gradle.taskGraph.whenReady {taskGraph ->
//    if (!taskGraph.hasTask(release)) version += "-SNAPSHOT"
//}

